/*
 * Copyright (C) 2013 University of South Florida (sjbarbeau@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenLocal()
    maven {
        // OBA Releases - for comparator to sort alphanumeric routes
        url "http://nexus.onebusaway.org/nexus/content/repositories/releases"
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 38
        versionName "1.7.2"
    }

    /**
     * brand - how the name, look, and feel of each OneBusAway-based app is presented
     * platform - Google or Amazon (for Maps API v2 dependencies)
     */
    flavorDimensions "brand", "platform"

    productFlavors {
        google {
            // Normal Google Play release - see src/google
            flavorDimension "platform"
        }

        amazon {
            /**
             * Amazon Fire Phone - see src/amazon
             *
             * If changes are made to classes in src/google/java/org/onebusaway/android/map package,
             * the same classes should be copied to src/amazon/java/org/onebusaway/android/map
             * package.  Then, the Google classes should be replaced with equivalent Amazon
             * classes as specified in http://bit.ly/AmazonMapsMigration.
             *
             * NOTE:  When doing this copying, ProprietaryMapHelpV2 should not need to be copied.
             * It's specific to each platform.
             */
            flavorDimension "platform"
        }

        oba {
            /**
             * Original OneBusAway brand
             *
             * To allow this app to be distributed via Google Play, the applicationId MUST remain the
             * original package name of the app before it was re-branded to OneBusAway:
             * com.joulespersecond.seattlebusbot
             */
            flavorDimension "brand"
            applicationId "com.joulespersecond.seattlebusbot"
            manifestPlaceholders = [databaseAuthority: "com.joulespersecond.oba"]
            // Overwrite default with original OBA authority
        }

        /**
         * Rebranded versions of OBA using brand build flavors are below.  To add a new brand:
         *  1. Add a new brand build flavor variant below
         *  2. Create a new folder src/{brandName}/res (or copy from one of the samples - src/agencyX or src/agencyY)
         *  3. Edit resource files in src/{brandName}/res subfolders with the brand information
         */

        agencyX {
            // "Agency X" rebranding OneBusAway as another app - see src/agencyX/res for branded resources
            flavorDimension "brand"
            applicationId "org.agencyx.android" // Unique listing of this brand on app store
            manifestPlaceholders = [databaseAuthority: applicationId.toString() + '.provider']
            // Set unique brand database authority in manifest
        }

        agencyY {
            // "Agency Y" rebranding OneBusAway as another app - see src/agencyY/res for branded resources
            flavorDimension "brand"
            applicationId "org.agencyy.android" // Unique listing of this brand on app store
            manifestPlaceholders = [databaseAuthority: applicationId.toString() + '.provider']
            // Set unique brand database authority in manifest
        }

        /**
         * Add more rebranding flavors here...
         */
    }

    /**
     * Set brand BuildConfig.DATABASE_AUTHORITY, used in org.onebusaway.android.provider.ObaContract
     */
    applicationVariants.all {
        variant ->
            def authority;
            if (variant.productFlavors.get(0).name == "oba") {
                authority = 'com.joulespersecond.oba' // Must keep the original OBA authority
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            } else {
                // It's cleaner to just append ".provider" to the applicationId for brand flavors
                authority = '"' + applicationId.toString() + '.provider' + '"'
                variant.buildConfigField "String", "DATABASE_AUTHORITY", authority
            }
    }

    // http://stackoverflow.com/questions/20673625/gradle-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    if (project.hasProperty("secure.properties")
            && new File(project.property("secure.properties")).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("secure.properties"))))

        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                storeFile file(props['key.store'])
                keyAlias props['key.alias']
                storePassword "askmelater"
                keyPassword "askmelater"
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile file("seattlebusbot3.debug.keystore")
            }

            release {
                // Nothing here
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}



task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.matches("\\bpackage\\w+Release\\b")) {
        theTask.dependsOn "askForPasswords"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'commons-io:commons-io:2.4'
    // JSON data binding for OBA REST API responses
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    // Bottom sliding panel
    compile 'com.sothree.slidinguppanel:library:2.0.3'
    // For sorting alphanumeric route names
    compile 'org.onebusaway.util:comparators:1.0.0'
    // Only used on Amazon devices via amazon build flavor
    amazonCompile('com.amazon.android:amazon-maps-api:2.0')
}
